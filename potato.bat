@echo off
setlocal enabledelayedexpansion

:: User Configurable Variables
set ADDON_NAME=LSPotato
set SOURCE_DIR=src
set DIST_DIR=dist
set DEFAULT_BLENDER_VERSION=5.1

:: Initialize paths
set ROOT_DIR=%~dp0
set BLENDER_BASE=%APPDATA%\Blender Foundation\Blender
set OPERATION=help

:: Get current Git branch
set GIT_BRANCH=unknown
git rev-parse --abbrev-ref HEAD 2>nul >nul && (
    for /f "delims=" %%b in ('git rev-parse --abbrev-ref HEAD') do set "GIT_BRANCH=%%b"
)

:: Parse command-line arguments
set AUTO_RELOAD=0
if not "%~1"=="" set OPERATION=%~1
:: Parse arg2/arg3 — accept --auto and blender version in either order
if not "%~2"=="" (
    if /i "%~2"=="--auto" (
        set AUTO_RELOAD=1
        if not "%~3"=="" set BLENDER_VERSION=%~3
    ) else (
        set BLENDER_VERSION=%~2
        if /i "%~3"=="--auto" set AUTO_RELOAD=1
    )
)
if not defined BLENDER_VERSION set BLENDER_VERSION=%DEFAULT_BLENDER_VERSION%

:: Calculate full paths
set FULL_SOURCE=%ROOT_DIR%%SOURCE_DIR%
set FULL_DIST=%ROOT_DIR%%DIST_DIR%
set ADDON_INSTALL_DIR=%BLENDER_BASE%\%BLENDER_VERSION%\scripts\addons

:: Verify source directory exists
if not exist "%FULL_SOURCE%\" (
    echo [ERROR]: Source directory not found at %FULL_SOURCE%
    exit /b 1
)

:: Main command router
if /i "%OPERATION%"=="reload" (
    if "%AUTO_RELOAD%"=="1" goto reload_auto
    goto reload
)
goto %OPERATION% 2>nul || goto help

:: Help documentation
:help
echo.
echo =============================================
echo   LSPotato Blender Addon - Build Script Help
echo =============================================
echo Location: %ROOT_DIR%
echo Branch: !GIT_BRANCH!
echo.
echo potato [command] [blender_version]
echo.
echo Commands:
echo   package     - Build addon zip package
echo   install     - Install to specified Blender version
echo   uninstall   - Remove from Blender addons directory
echo   clean       - Clean build artifacts
echo   test        - Run code checks (requires flake8)
echo   dev         - Clean + package + install
echo   reload      - Uninstall + dev
echo   └── --auto  - Watch for changes and automatically reload
echo.
echo Blender Version:
echo   Default: %DEFAULT_BLENDER_VERSION%
echo   Current: %BLENDER_VERSION%
echo   Install path: %ADDON_INSTALL_DIR%
echo.
echo Examples:
echo   potato install
echo   potato install 3.6
echo   potato uninstall 4.0
echo ===============================================
goto :eof

:: Clean build artifacts
:clean
echo.
echo [INFO] Cleaning build artifacts...
if exist "%FULL_DIST%" rmdir /s /q "%FULL_DIST%"
if exist "%FULL_SOURCE%\*.pyc" del /s /q "%FULL_SOURCE%\*.pyc"
echo [SUCCESS] Clean dist done.
goto :eof

:: Remove stale generated node files from source
:cleanup_stale_nodes
set "RAMP_FILE=%FULL_SOURCE%\nodes\shader\lscherry\build_face_ramp.py"
set "INIT_FILE=%FULL_SOURCE%\nodes\shader\lscherry\__init__.py"
if exist "%RAMP_FILE%" (
    del /q "%RAMP_FILE%"
    echo [INFO] Removed stale: nodes\shader\lscherry\build_face_ramp.py
)
if exist "%INIT_FILE%" (
    powershell -NoProfile -Command "$f='%INIT_FILE%'; $lines=(Get-Content $f) | Where-Object { $_ -notmatch 'from \.build_face_ramp import' }; $lines | Set-Content $f -Encoding utf8"
)
goto :eof

:: Build addon zip package
:package
call :cleanup_stale_nodes
call :clean
echo.
echo [INFO] Packaging addon [!GIT_BRANCH!]...
if not exist "%FULL_DIST%" mkdir "%FULL_DIST%"

:: Create zip file at: dist\LSPotato_1.0.0.zip
set "ZIP_PATH=%FULL_DIST%\%ADDON_NAME%_!GIT_BRANCH!.zip"

python package.py "%FULL_SOURCE%" "%ZIP_PATH%"

if not exist "%ZIP_PATH%" (
    echo [ERROR] Failed to create zip package
    exit /b 1
)
echo [INFO] Created: %ZIP_PATH%
goto :eof

:: Install to Blender
:install
call :package
echo.
echo [INFO] Installing [!GIT_BRANCH!] to Blender %BLENDER_VERSION%...

:: Verify Blender directory exists
if not exist "%ADDON_INSTALL_DIR%" (
    echo [ERROR] Blender %BLENDER_VERSION% not found at: "%ADDON_INSTALL_DIR%" 
    echo [ERROR] Please verify Blender version and installation
    exit /b 1
)

:: Install addon (preserve existing __init__.py)
if not exist "%ADDON_INSTALL_DIR%\%ADDON_NAME%" mkdir "%ADDON_INSTALL_DIR%\%ADDON_NAME%"
xcopy /Y /E /Q "%FULL_SOURCE%\*" "%ADDON_INSTALL_DIR%\%ADDON_NAME%\"

echo [INFO] Installed to: "%ADDON_INSTALL_DIR%\%ADDON_NAME%"
echo [INFO] Branch: !GIT_BRANCH!
echo [INFO] Please restart Blender to activate the addon
goto :eof

:: Remove from Blender
:uninstall
echo.
echo [SUCCESS] Uninstalling from Blender %BLENDER_VERSION%...
set ADDON_PATH="%ADDON_INSTALL_DIR%\%ADDON_NAME%"

if exist %ADDON_PATH% (
    rmdir /s /q %ADDON_PATH%
    echo [INFO] Removed: %ADDON_PATH%
) else (
    echo [ERROR] Addon not found: %ADDON_PATH%
)
goto :eof

:: Run code checks
:test
echo.
echo Running code checks...
flake8 "%FULL_SOURCE%"
if errorlevel 1 (
    echo [ERROR] Code checks failed
    exit /b 1
) else (
    echo [SUCCESS] All tests passed!
)
goto :eof

:: Development shortcut
:dev
call :clean
call :install
echo.
echo [SUCCESS] Development cycle complete for [!GIT_BRANCH!]!
goto :eof

:: Quickly Development shortcut
:reload
call :uninstall
call :install
echo.
echo [SUCCESS] Development cycle complete for [!GIT_BRANCH!]!
echo !! Happy Cherrying !!
echo.
goto :eof

:: Auto-reload: watch src/ and reload on every change
:reload_auto
echo.
echo [INFO] Auto-reload mode [!GIT_BRANCH!] ^| Blender %BLENDER_VERSION%
echo [INFO] Watching: %FULL_SOURCE%
echo [INFO] Press Ctrl+C to stop.
echo.
call :reload
echo.
echo [INFO] Watching for changes...
powershell -NoProfile -ExecutionPolicy Bypass -Command "$w=New-Object IO.FileSystemWatcher '%FULL_SOURCE%';$w.IncludeSubdirectories=$true;$w.EnableRaisingEvents=$true;$global:c=$false;$a={$n=$Event.SourceEventArgs.Name;if(-not($n.EndsWith('.pyc')-or$n-like'*__pycache__*')){$global:c=$true}};Register-ObjectEvent $w Changed -Action $a|Out-Null;Register-ObjectEvent $w Created -Action $a|Out-Null;Register-ObjectEvent $w Deleted -Action $a|Out-Null;Register-ObjectEvent $w Renamed -Action $a|Out-Null;while($true){Start-Sleep -Milliseconds 500;if($global:c){$global:c=$false;Write-Host '[WATCH] Change detected, waiting for writes to settle...';Start-Sleep -Seconds 1;exit 2}}"
if errorlevel 2 goto reload_auto
goto :eof

:: End of script
:eof
endlocal